
&НаКлиенте
Процедура СтрНайтиПоРегВыражению(Команда)
	
	Если ПустаяСтрока(ШаблонПоиска)
		ИЛИ ПустаяСтрока(ТекстПоиска) Тогда
		Сообщить("Не указан шаблон поиска или текст");
		Возврат;
	КонецЕсли;
	
	СтрНайтиПоРегВыражениюНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СтрНайтиПоРегВыражениюНаСервере()
	
	УстановитьЗаголовок(1);
	
	РезультатПоиска.Очистить();
	
	НаправлениеПоиск = ОпределитьНаправлениеПоиска();
	
	Если Направление = 1 Тогда
		НачальнаяПозиция = 1;
	Иначе
		НачальнаяПозиция = СтрДлина(ТекстПоиска.ПолучитьТекст());
	КонецЕсли;
	
	Результат = СтрНайтиПоРегулярномуВыражению(ТекстПоиска.ПолучитьТекст(),
		ШаблонПоиска,
		НаправлениеПоиск,
		НачальнаяПозиция,
		НомерВхождения,
		ИгнорироватьРегистр,
		МногострочныйПоиск);
	
	Если Результат.НачальнаяПозиция = 0 Тогда
		ТекстДляУстановки = "Шаблон не найден";
	Иначе
		НомерСтроки = ПолучитьНомерСтрокиМодуля(ТекстПоиска.ПолучитьТекст(), Результат.НачальнаяПозиция);
		
		ТекстДляУстановки = СтрШаблон("Длина: %1
		|Значение: %2
		|Начальная позиция: %3
		|Номер строки: %4",
		Результат.Длина,
		Результат.Значение,
		Результат.НачальнаяПозиция,
		НомерСтроки);
	КонецЕсли;
	
	РезультатПоиска.УстановитьТекст(ТекстДляУстановки);
	
КонецПроцедуры

&НаКлиенте
Процедура СтрНайтиВсеПоРегВыражению(Команда)
	
	Если ПустаяСтрока(ШаблонПоиска)
		ИЛИ ПустаяСтрока(ТекстПоиска) Тогда
		Сообщить("Не указан шаблон поиска или текст");
		Возврат;
	КонецЕсли;
	
	СтрНайтиВсеПоРегВыражениюНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СтрНайтиВсеПоРегВыражениюНаСервере()
	
	УстановитьЗаголовок(1);
	
	РезультатПоиска.Очистить();
	
	Результат = СтрНайтиВсеПоРегулярномуВыражению(ТекстПоиска.ПолучитьТекст(), ШаблонПоиска, ИгнорироватьРегистр, МногострочныйПоиск);
	
	Если Результат.Количество() = 0 Тогда
		
		ТекстДляУстановки = "Шаблон не найден";
		
	Иначе
		
		ТекстДляУстановки = "";
		Для Каждого НайденнаяСтрока Из Результат Цикл
			
			НомерСтроки = ПолучитьНомерСтрокиМодуля(ТекстПоиска.ПолучитьТекст(), НайденнаяСтрока.НачальнаяПозиция);
			
			Шаблон = СтрШаблон("Значение: %1
			|Номер строки: %2%3%3",
			НайденнаяСтрока.Значение,
			НомерСтроки,
			Символы.ПС);
			
			ТекстДляУстановки = ТекстДляУстановки + Шаблон;
			
		КонецЦикла;
		
		ТекстШапка = СтрШаблон(НСтр("ru='Найдено всего: %1%2%2'"), Результат.Количество(), Символы.ПС);
		ТекстДляУстановки = ТекстШапка + ТекстДляУстановки;
		
	КонецЕсли;
	
	РезультатПоиска.УстановитьТекст(ТекстДляУстановки);
	
КонецПроцедуры

&НаКлиенте
Процедура СтрЗаменитьПоРегВыражению(Команда)
	
	Если ПустаяСтрока(ШаблонПоиска)
		ИЛИ ПустаяСтрока(ТекстПоиска) Тогда
		Сообщить("Не указан шаблон поиска или текст");
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ШаблонЗамены) Тогда
		Сообщить("Не указан шаблон замены");
		Возврат;
	КонецЕсли;
	
	СтрЗаменитьПоРегВыражениюНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СтрЗаменитьПоРегВыражениюНаСервере()
	
	УстановитьЗаголовок(2);
	
	РезультатПоиска.Очистить();
	
	Результат = СтрЗаменитьПоРегулярномуВыражению(ТекстПоиска.ПолучитьТекст(), ШаблонПоиска, ШаблонЗамены, ИгнорироватьРегистр, МногострочныйПоиск);
	РезультатПоиска.УстановитьТекст(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура СтрПодобнаПоРегВыражению(Команда)
	
	Если ПустаяСтрока(ШаблонПоиска)
		ИЛИ ПустаяСтрока(ТекстПоиска) Тогда
		Сообщить("Не указан шаблон поиска или текст");
		Возврат;
	КонецЕсли;
	
	СтрПодобнаПоРегВыражениюНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СтрПодобнаПоРегВыражениюНаСервере()
	
	УстановитьЗаголовок(3);
	
	РезультатПоиска.Очистить();
	
	Результат = СтрПодобнаПоРегулярномуВыражению(ТекстПоиска.ПолучитьТекст(), ШаблонПоиска, ИгнорироватьРегистр, МногострочныйПоиск);
	РезультатПоиска.УстановитьТекст(Результат);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовок(НомерПараметра)
	
	Если НомерПараметра = 1 Тогда
		Элементы.Результат.Заголовок = "Результат поиска";
	ИначеЕсли НомерПараметра = 2 Тогда
		Элементы.Результат.Заголовок = "Результат замены";
	ИначеЕсли НомерПараметра = 3 Тогда
		Элементы.Результат.Заголовок = "Результат сравнения";
	Иначе
		Элементы.Результат.Заголовок = "Найденные группы";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьНомерСтрокиМодуля(ТекстМодуля, НомерСимвола)
	
	ТекстДоСимвола = Лев(ТекстМодуля, НомерСимвола);
	НомерСтроки = СтрЧислоСтрок(ТекстДоСимвола);
	
	Возврат НомерСтроки;
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИгнорироватьРегистр = Истина;
	МногострочныйПоиск = Истина;
	УстановитьЗаголовок(1);
	ЗаполнитьЗначенияПоУмолчанию();
	
	ВидыПоиска = 1;
	УстановитьТекст();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекст()
	
	Если ВидыПоиска = 1 Тогда
		ТекстПоиска.УстановитьТекст("Съешь ещё этих мягких французских булок да выпей чаю.");
	ИначеЕсли ВидыПоиска = 2 Тогда
		СписокПЯ = "aleksey.sidorov@1C.com
		|иван_бутылкин_777@купим-стеклотару-дорого.рф
		|master-lomaster@muzhchina_na_chas.ru";
		ТекстПоиска.УстановитьТекст(СписокПЯ);
	Иначе
		СписокТелефонов = "+7-123-321-44-55
		|8(912)1597538
		|+8 456 123 45 78
		|+8(159)789 12 98
		|+7(123)-321-44-55";
		ТекстПоиска.УстановитьТекст(СписокТелефонов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыПоискаПриИзменении(Элемент)
	
	УстановитьТекст();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗначенияПоУмолчанию()
	
	Направление = 1;
	НачальнаяПозиция = 1;
	НомерВхождения = 1;
	
КонецПроцедуры

&НаСервере
Функция ОпределитьНаправлениеПоиска()
	
	Если Направление = 1 Тогда
		НаправлениеПоиск = НаправлениеПоиска.СНачала;
	Иначе
		НаправлениеПоиск = НаправлениеПоиска.СКонца;
	КонецЕсли;
	
	Возврат НаправлениеПоиск;
	
КонецФункции

&НаКлиенте
Процедура СтрНайтиПоРегВыражениюГруппы(Команда)
	СтрНайтиПоРегВыражениюГруппыНаСервере();
КонецПроцедуры

&НаСервере
Процедура СтрНайтиПоРегВыражениюГруппыНаСервере()
	
	УстановитьЗаголовок(4);
	
	РезультатПоиска.Очистить();
	
	НаправлениеПоиск = ОпределитьНаправлениеПоиска();
	
	Если Направление = 1 Тогда
		НачальнаяПозиция = 1;
	Иначе
		НачальнаяПозиция = СтрДлина(ТекстПоиска.ПолучитьТекст());
	КонецЕсли;
	
	Результат = СтрНайтиПоРегулярномуВыражению(ТекстПоиска.ПолучитьТекст(),
		ШаблонПоиска,
		НаправлениеПоиск,
		НачальнаяПозиция,
		НомерВхождения,
		ИгнорироватьРегистр,
		МногострочныйПоиск);
	
	Если Результат.НачальнаяПозиция = 0 Тогда
		ТекстДляУстановки = "Шаблон не найден";
	Иначе
		
		ГруппыРВ = Результат.ПолучитьГруппы();
		ТекстДляУстановки = "";
		Счетчик = 1;
		Для Каждого НайденнаяСтрока Из ГруппыРВ Цикл
			
			Шаблон = СтрШаблон("Группа %1: %2%3%3",
			Счетчик,
			НайденнаяСтрока.Значение,
			Символы.ПС);
			
			ТекстДляУстановки = ТекстДляУстановки + Шаблон;
			Счетчик = Счетчик + 1;
		КонецЦикла;
		
		ТекстШапка = СтрШаблон(НСтр("ru='Найдено всего: %1%2%2'"), ГруппыРВ.Количество(), Символы.ПС);
		ТекстДляУстановки = ТекстШапка + ТекстДляУстановки;
	КонецЕсли;
	
	РезультатПоиска.УстановитьТекст(ТекстДляУстановки);
	
КонецПроцедуры
